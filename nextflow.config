// Nextflow config for use with PDC at KTH

def cluster = "unknown"

try {
    cluster = ['/bin/bash', '-c', 'sacctmgr show cluster -n | grep -o "^\s*[^ ]*\s*"'].execute().text.trim()
} catch (java.io.IOException e) {
    System.err.println("WARNING: Could not run scluster, defaulting to unknown")
}

params {
    config_profile_description = 'PDC profile.'
    config_profile_contact = 'Pontus Freyhult (@pontus)'
    config_profile_url = "https://www.pdc.kth.se/"

    max_memory = 1790.GB
    max_cpus = 256
    max_time = 7.d

    schema_ignore_params = "genomes,input_paths,cluster-options,clusterOptions,validationSchemaIgnoreParams"
    validationSchemaIgnoreParams = "genomes,input_paths,cluster-options,clusterOptions,schema_ignore_params"

    executor = "slurm"
    fastqc_version = "0.12.1"
    trimmomatic_version = "0.39"
    stacks_version = "2.65"
    multiqc_version = "1.24.1"

    trimmomatic_cpus = 4
    trimmomatic_adapters = "$baseDir/data/adapters/TruSeq3-PE.fa"
    trimmomatic_trimmers = [
        "HEADCROP:5", "ILLUMINACLIP:${  trimmomatic_adapters}:2:30:10",
        "LEADING:3", "TRAILING:3",
        "MINLEN:140", "CROP:140"
    ]

    process_radtags_params = [
        "-i gzfastq", "--quality", "--disable-rad-check", "-e ecoRI"
     ]

    ustacks_cpus = 4
    ustacks_params = [""]

    cstacks_cpus = 8
    cstacks_popmap = "$baseDir/data/popmap.tsv"
    cstacks_params = ["-n 2"]

    sstacks_cpus = 8
    sstacks_params = [""]

    tsv2bam_cpus = 8
    tsv2bam_params = [""]

    gstacks_cpus = 8
    gstacks_params = [""]

    populations_cpus = 8
    populations_params = [""]

    multiqc_params = [""]
}



def containerOptionsCreator = {
    switch(cluster) {
        case "dardel":
            return '-B /cfs/klemming/'
    }

    return ''
}

def clusterOptionsCreator = { mem, time, cpus ->
    String base = "-A naiss2023-22-923  ${params.clusterOptions ?: ''}"

    switch(cluster) {
        case "dardel":
            String extra = ''

            if (time < 7.d && mem <= 222.GB && cpus < 256) {
                extra += ' -p shared '
            }
            else if (time < 1.d) {
                // Shortish
                if (mem > 222.GB) {
                    extra += ' -p memory,main '
                } else {
                    extra += ' -p main '
                }
            } else {
                // Not shortish
                if (mem > 222.GB) {
                    extra += ' -p memory '
                } else {
                    extra += ' -p long '
                }
            }

            if (!mem || mem < 6.GB) {
                // Impose minimum memory if request is below
                extra += ' --mem=6G '
            }

            return base+extra
    }

    return base
}


singularity {
    enabled = true
    runOptions = containerOptionsCreator()
}

process {
    // Should we lock these to specific versions?
    beforeScript = 'module load PDC apptainer'
    
    memory = { 20.GB * task.attempt }
    time = { 48.h * task.attempt }

    executor = 'slurm'
    clusterOptions = { clusterOptionsCreator(task.memory, task.time, task.cpus) }

    withName: 'tsv2bam' {
        memory = 60.GB
        clusterOptions = '-A naiss2023-22-923 -n 40 -p shared --mem 60G -t 12:00:00'
    }
}
